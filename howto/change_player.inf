!% -Cu
!% -~S
!% $OMIT_UNUSED_ROUTINES=1
!% $ZCODE_LESS_DICT_DATA=1
!% $TRANSCRIPT_FORMAT=1

Constant Story      "Cambia Personaje";
Constant Headline   "^Un juego de muestra que usa PunyInformES.^";

Constant STATUSLINE_SCORE; Statusline score;

Constant INITIAL_LOCATION_VALUE = SalaBlanca;
Constant CUSTOM_PLAYER_OBJECT = Bob; ! Establecer el objeto jugador inicial a uno de nuestros
                                     ! personajes - no importa cual, para evitar
                                     ! crear un objeto personaje que no será usado.

Include "globals.h";

Include "puny.h";

[ SwitchFromPlayerCharacter p_old_pc _i;
!	give p_old_pc ~concealed ~transparent;
	give p_old_pc ~concealed;
	for(_i = 0 : _i < 3 : _i++)
		p_old_pc.&name-->_i = p_old_pc.&name_other-->_i;
	p_old_pc.short_name = p_old_pc.short_name_other;
	p_old_pc.description = p_old_pc.description_other;
];

[ SwitchToPlayerCharacter p_new_pc;
	give p_new_pc concealed transparent;
	p_new_pc.&name-->0 = 'yo';
	p_new_pc.&name-->1 = 'mi';
	p_new_pc.&name-->2 = 'mismo';
	p_new_pc.short_name = "tú mismo";
	p_new_pc.description = p_new_pc.description_player;
	player = p_new_pc;
	location = superparent(p_new_pc);
	real_location = location;
	_UpdateDarkness(true);
	MoveFloatingObjects();
];

[ ChangePlayer p_new_pc p_first_time _character;
	if(player ofclass PlayableCharacter && p_first_time == 0) {
		SwitchFromPlayerCharacter(player);
	} else {
		! Esta es la primera vez que se llama a ChangePlayer. Necesitamos cambiar
		! al modo no activo para todos los personajes jugadores no activos.
		objectloop(_character ofclass PlayableCharacter && _character ~= p_new_pc) {
			SwitchFromPlayerCharacter(_character);
		}
	}
	SwitchToPlayerCharacter(p_new_pc);
];

Class PlayableCharacter
	with
		parse_name [ _words;
			while(NextWordStopped() == self.&name-->0 or self.&name-->1 or
					self.&name-->2 or self.&name_other-->0 or
					self.&name_other-->1 or	self.&name_other-->2) _words++;
			return _words;
		],
		life [;
			Give:
				move noun to self;
				print "~Well thank you very much ";
				PrintOrRun(player, short_name_other, true);
				"!~";
		],
		name 'yo' 'mi' 'mismo',
		name_other 0 0 0,
		short_name  "tú mismo",
		short_name_other 0,
		description 0,
		description_other 0,
		description_player "Tan buen aspecto como siempre.",
		capacity MAX_CARRIED,
	has animate proper concealed transparent;

Object SalaBlanca "Sala blanca"
	with
		description "Te encuentras en una gran sala blanca. Al sur hay una habitación azul.",
		s_to SalaAzul,
	has light;

Object -> Mesa "mesa"
	with
		name 'mesa',
	has static supporter enterable female;

Object -> -> plato "plato"
	with
		name 'plato',
	has supporter;

Object -> -> "tenedor"
	with
		name 'tenedor';

Object -> Bob "tú" class PlayableCharacter
	with
		name_other 'bob' 'bob' 'bob',
		short_name_other  "Bob",
		description_player "Eres guapo y lo sabes.",
		description_other "Bob es guapo, eso seguro.";

Object SalaAzul "Sala azul"
	with
		description "Estás en una pequeña sala azul. Al norte hay una habitación blanca.",
		n_to SalaBlanca,
	has light;

Object -> Sally "tú" class PlayableCharacter
	with
		name_other 'sally' 'sally' 'sally',
		short_name_other  "Sally",
		description_player "Crees que estás bien.",
		description_other "Sally se ve bien."
	has female;

[ PlayableCharacterScope _obj;
	switch(scope_stage) {
	1:
		rfalse;
	2:
		objectloop(_obj ofclass PlayableCharacter) {
			PlaceInScope(_obj);
		}
		rtrue;
	3:
		"¡No puedes convertirte en eso!";
	}
];

[BecomeSub;
	if(noun == player)
	 	"¡Ya lo eres!";
	ChangePlayer(noun);
	if(AfterRoutines()) rtrue;
	print "Ok, ahora eres ";
	PrintOrRun(noun, short_name_other, true);
	print ".^";
	<Look>;
];

Verb 'convertirte' 'conviertete'
	* 'en' scope=PlayableCharacterScope -> Become;

[Initialise;
	ChangePlayer(Bob, true);
	! En la rutina Initialise, debes quitar al personaje si quieres obtener el Mirar inicial.
	! La biblioteca volverá a colocar al jugador en la 'ubicación'.
	remove player;
	print "^^En esta demo puedes cambiar de personaje escribiendo ~CONVIERTETE EN SALLY~ o ~CONVIERTETE EN BOB~.^^";
];
