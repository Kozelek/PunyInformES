!% -Cu
!% -~S
!% $OMIT_UNUSED_ROUTINES=1
!% $ZCODE_LESS_DICT_DATA=1
!% $MAX_ABBREVS=96
!% $TRANSCRIPT_FORMAT=1
!!% $ZCODE_MAX_INLINE_STRING=9999    ! Requiere Inform 6.42
!!% $OMIT_SYMBOL_TABLE=1             ! Requiere Inform 6.42, y no puede usarse con DEBUG

! Las primeras líneas del archivo de código fuente principal de un juego pueden
! contener opciones de compilador, como las líneas anteriores.
! -Cu permite que se usen caracteres acentuados.
! -~S desactiva la comprobación estricta de errores. Esto se utiliza en los juegos z5 y z8
! por defecto. Aunque es útil para la depuración, añade ~10 KB al tamaño del archivo de la
! historia y hace que el juego sea más lento.
! $OMIT_UNUSED_ROUTINES=1 hace que el compilador elimine todas las rutinas que
! no se usan. Esto puede ahorrar algo de espacio.
! $ZCODE_LESS_DICT_DATA=1 elimina un byte de datos vacío por cada palabra del diccionario
! $MAX_ABBREVS=96 aumenta el número máximo de abreviaturas de 64 a 96, eliminando la opción
! de usar 32 cadenas dinámicas.
! @ZCODE_MAX_INLINE_STRING=9999 hace que el compilador coloque todas las cadenas usadas en
! código en línea con el código, en lugar de colocar las cadenas más largas en el área de
! cadenas de memoria alta. Esto ahorra algunos bytes. 

! A menos que se compile en modo DEBUG, también se puede definir $OMIT_SYMBOL_TABLE=1 para
! decirle al compilador que no incluya nombres de propiedades, atributos y demás,
! ahorrando así algo de espacio.

! Abbreviaciones creadas con ZAbbrevMaker 0.11 - https://github.com/heasm66/ZAbbrevMaker
Constant CUSTOM_ABBREVIATIONS;
Abbreviate ". La única salida es";          !     3x25, saved    42
Abbreviate " nada inesperado.";             !     3x18, saved    30
Abbreviate "caja de cartón";                !     4x17, saved    42
Abbreviate "la espalda y ";                 !     3x13, saved    18
Abbreviate "la oscuridad";                  !     6x12, saved    48
Abbreviate "os símbolos ";                  !     4x15, saved    37
Abbreviate "a habitación";                  !    18x15, saved   219
Abbreviate "Sin embargo,";                  !     3x14, saved    21
Abbreviate "a biblioteca";                  !    20x12, saved   188
Abbreviate " estantería";                   !     7x14, saved    69
Abbreviate " dentro de ";                   !     6x11, saved    42
Abbreviate "as paredes";                    !    12x10, saved    84
Abbreviate "as llamas";                     !     8x 9, saved    47
Abbreviate "encargado";                     !     9x 9, saved    54
Abbreviate " ardiendo";                     !     5x 9, saved    26
Abbreviate "edificio";                      !     6x 8, saved    27
Abbreviate "escalera";                      !     9x 8, saved    45
Abbreviate "necesit";                       !     6x 7, saved    21
Abbreviate "también";                       !     6x10, saved    36
Abbreviate ", señor";                       !     8x11, saved    60
Abbreviate " parece";                       !    21x 7, saved    96
Abbreviate " primer";                       !     6x 7, saved    21
Abbreviate "puedes ";                       !    17x 7, saved    76
Abbreviate "sótano";                        !     8x 9, saved    47
Abbreviate " sobre";                        !    12x 6, saved    42
Abbreviate " oscur";                        !     9x 6, saved    30
Abbreviate " para ";                        !    14x 6, saved    50
Abbreviate "cerrad";                        !     9x 6, saved    30
Abbreviate "as de ";                        !    13x 6, saved    46
Abbreviate "trabaj";                        !     9x 6, saved    30
Abbreviate "ciones";                        !     7x 6, saved    22
Abbreviate " ahora";                        !     9x 6, saved    30
Abbreviate " hacia";                        !     7x 6, saved    22
Abbreviate "pequeñ";                        !     5x 9, saved    26
Abbreviate " una ";                         !    30x 5, saved    84
Abbreviate "Frank";                         !    13x 6, saved    46
Abbreviate "mente";                         !    28x 5, saved    78
Abbreviate "direc";                         !    10x 5, saved    24
Abbreviate " del ";                         !    34x 5, saved    96
Abbreviate " nada";                         !    18x 5, saved    48
Abbreviate " está";                         !    39x 8, saved   225
Abbreviate " más ";                         !    13x 8, saved    69
Abbreviate " por ";                         !    20x 5, saved    54
Abbreviate "ando ";                         !    26x 5, saved    72
Abbreviate "libro";                         !    23x 5, saved    63
Abbreviate " aquí";                         !    10x 8, saved    51
Abbreviate " de l";                         !    35x 5, saved    99
Abbreviate " que ";                         !    70x 5, saved   204
Abbreviate " un ";                          !    32x 4, saved    58
Abbreviate " el ";                          !    58x 4, saved   110
Abbreviate "ante";                          !    21x 4, saved    36
Abbreviate " con";                          !    57x 4, saved   108
Abbreviate "ción";                          !    19x 7, saved    86
Abbreviate " la ";                          !    32x 4, saved    58
Abbreviate " en ";                          !    43x 4, saved    80
Abbreviate " de ";                          !    68x 4, saved   130
Abbreviate "_to/";                          !    12x 6, saved    42
Abbreviate " des";                          !    24x 4, saved    42
Abbreviate " no ";                          !    25x 4, saved    44
Abbreviate "uego";                          !    21x 4, saved    36
Abbreviate "ero ";                          !    19x 4, saved    32
Abbreviate "ien";                           !    37x 3, saved    34
Abbreviate "lo ";                           !    25x 3, saved    22
Abbreviate "ida";                           !    20x 3, saved    17
Abbreviate "de ";                           !    25x 3, saved    22
Abbreviate "ent";                           !    71x 3, saved    68
Abbreviate "aña";                           !     9x 6, saved    30
Abbreviate "No ";                           !    52x 4, saved    98
Abbreviate "er ";                           !    26x 3, saved    23
Abbreviate "as ";                           !   119x 3, saved   116
Abbreviate "sal";                           !    22x 3, saved    19
Abbreviate "ert";                           !    21x 3, saved    18
Abbreviate "est";                           !    52x 3, saved    49
Abbreviate "ter";                           !    29x 3, saved    26
Abbreviate "per";                           !    25x 3, saved    22
Abbreviate "es ";                           !    35x 3, saved    32
Abbreviate "El ";                           !    19x 4, saved    32
Abbreviate " a ";                           !    43x 3, saved    40
Abbreviate "ado";                           !    43x 3, saved    40
Abbreviate "os ";                           !    46x 3, saved    43
Abbreviate "se ";                           !    38x 3, saved    35
Abbreviate "te ";                           !    48x 3, saved    45
Abbreviate " al";                           !    53x 3, saved    50
Abbreviate "a.";                            !    24x 3, saved    21
Abbreviate ", ";                            !    87x 3, saved    84
Abbreviate "o.";                            !    47x 3, saved    44
Abbreviate "Es";                            !    31x 3, saved    28
Abbreviate ". ";                            !   104x 3, saved   101
Abbreviate "^^";                            !    21x 4, saved    36
Abbreviate "á";                             !    46x 4, saved    86
Abbreviate "ú";                             !    12x 4, saved    18
Abbreviate "í";                             !    66x 4, saved   126
Abbreviate "¡";                             !    28x 4, saved    50
Abbreviate "¿";                             !    13x 4, saved    20
Abbreviate "ó";                             !    20x 4, saved    34
Abbreviate "é";                             !    29x 4, saved    52

! Library_of_horror.inf - un juego de ejemplo que usa PunyInform


! ====================================
! Configurar constantes para personalizar PunyInform
! ====================================

! Descomentar para añadir características opcionales a PunyInform
!Constant DEBUG;
!Constant OPTIONAL_EXTENDED_METAVERBS;
!Constant OPTIONAL_EXTENDED_VERBSET;
!Constant OPTIONAL_GUESS_MISSING_NOUN;
Constant OPTIONAL_FULL_SCORE;
Constant OPTIONAL_MANUAL_SCOPE;
Constant OPTIONAL_MANUAL_SCOPE_BOOST;
Constant OPTIONAL_REACTIVE_PARSE_NAME;
! 0 = Comprobación mínima de errores, 1 = Códigos de error numéricos, 2: Mensajes de error
Constant RUNTIME_ERRORS = 0;

Constant INITIAL_LOCATION_VALUE = GloomyStreet;

Constant STATUSLINE_SCORE; Statusline score;

Constant Story	  "La biblioteca de los horrores";
Constant Headline   "^Un juego de demostración de PunyInform, por Pablo Martínez y Fredrik Ramsberg.^";
Release 10;
Serial "231010";

! Obtén tu propio IFID para tu juego en https://www.tads.org/ifidgen/ifidgen
Array UUID_ARRAY string "UUID://24F3E924-37A0-4771-A9DF-376476D69ACE//"; #Ifdef UUID_ARRAY; #Endif;

Constant MAX_SCORE = 100;
Constant TASKS_PROVIDED;
Constant NUMBER_TASKS = 5;
Array task_scores -> 10 20 20 50 20;
Constant TASK_TALK_MANAGER = 0;
Constant TASK_GIVE_BOOK = 1;
Constant TASK_BLOCK_ROBOT = 2;
Constant TASK_BURN_BOOK = 3;
Constant TASK_LEAVE = 4;

Constant MAX_SCOPE = 10;
Constant MAX_TIMERS = 2;

! ====================================
! Incluye globals.h
! ====================================
Include "globals.h";

! ====================================
! Define las rutinas de entrada a la biblioteca
! ====================================
[ PrintRank;
	print ", otorgándote el rango de ";
	if (score >= 100) "Héroe.";
	if (score >= 70) "Valiente.";
	if (score >= 50) "Confiable.";
	if (score >= 30) "Prometedor.";
	if (score >= 10) "Bienintencionado.";
	"Novato.";
];
[ PrintTaskName task_number;
	switch (task_number) {
		TASK_TALK_MANAGER: 	"por hablar con el encargado";
		TASK_GIVE_BOOK: 	"por dar el libro al encargado";
		TASK_BLOCK_ROBOT: 	"por obstaculizar al robot";
		TASK_BURN_BOOK: 	"por quemar el libro maléfico";
		TASK_LEAVE: 		"por salir de la biblioteca";
	}
];


[ DeathMessage;
	switch(deadflag) {
		3: print "Has invocado demonios dispuestos a destruir el mundo";
		4: print "Has sido incinerado";
	}
];

#IfV3;
! El diccionario sólo admite seis caracteres en los juegos z3, por lo que necesitamos
! para ayudar a imprimir verbos más largos.
[PrintVerb p_verb;
	switch(p_verb) {
		'vendar': print "vendar los ojos"; rtrue;
	}
	rfalse;
];
#EndIf;

! ====================================
! Configura banderas
! ====================================
Constant FLAG_COUNT = 5;
Constant F_TALKED_TO_MANAGER = 1;
Constant F_FRANK_BLOCKED_BY_BOX = 2;
Constant F_BOOK_DELIVERED = 3;
Constant F_STARTED_FIRE = 4;
Constant F_FRANK_HAS_SEEN_BOOK = 5;
Include "ext_flags.h";

! ====================================
! Configura cheap scenery
! ====================================
[SceneryReply p_word1 p_word2 p_id_or_routine;
	default:
		p_word1 = p_word2 + p_id_or_routine; ! Evita advertencias del compilador por variables no utilizadas
		"Mejor déjalo estar y sigue con la tarea.";
];
Include "ext_cheap_scenery.h";

! ====================================
! Configura quote box
! ====================================
#Ifv3;
Constant QUOTE_V3_SCREEN_WIDTH 40;
#Endif;
Include "ext_quote_box.h";

Array quote_stairs static --> 5 34
"Es al descender al abismo cuando"
"recuperamos los tesoros de la"
"vida. Donde tropiezas, allí"
"yace tu tesoro."
"              -- Joseph Campbell";

Array quote_darkchamber static --> 6 35
"La luz cree que viaja más rápido que"
"cualquier cosa, pero se equivoca. No"
"importa cuán rápido viaje, siempre"
"descubre que la oscuridad ha llegado"
"antes, y la está esperando."
"              -- Terry Pratchett";




! ====================================
! Incluye puny.h
! ====================================
Include "puny.h";

! ====================================
! Clases, verbos etc para el juego
! ====================================

! -- testing scope grammar token
! See: https://www.inform-fiction.org/manual/html/s32.html

! Quemar está disponible en el conjunto de verbos ampliado, pero lo definiremos aquí
! en lugar de añadir el peso de todos esos nuevos verbos. Además, queremos que
! funcione de forma un poco diferente a como funciona en la biblioteca -
! el jugador no debería tener que llevar las velas para quemar el libro.

[ BurnSub;
	"Eso parece peligroso e inútil.";
];

Verb 'quema' 'quemar'
	* noun					-> Burn
	* noun 'con' noun		-> Burn;

Extend 'enciende' first
	* noun					-> Burn
	* noun 'con' noun		-> Burn;

Extend 'encender' first
	* noun					-> Burn
	* noun 'con' noun		-> Burn;

[ CoverSub;
	"Eso parece inútil.";
];


Extend only 'cubrir' 'cubre' replace
	* noun 'con'/'usando' held	-> Cover;

Verb 'venda' = 'cubre';
Verb 'vendar' = 'cubrir';

Extend 'pon' first
	* held 'en' creature		-> Cover reverse
	* held 'sobre' noun 		-> Cover reverse;
Extend 'poner' first
	* held 'en' creature		-> Cover reverse
	* held 'sobre' noun 		-> Cover reverse;

[ TalkToSub;
	print_ret (The) noun, " no parece", (_n) noun ," interesad", (_o) noun ,".";
];

Extend 'habla' first
	* 'a'/'con' creature -> TalkTo
	* creature -> TalkTo;
Extend 'hablar' first
	* 'a'/'con' creature -> TalkTo
	* creature -> TalkTo;


! ====================================
! El siguiente código, fuertemente inspirado en la extensión cheap_scenery,
! permite al jugador hacer preguntas como "¿QUÉ ES UNA BIBLIOTECA?" sin
! usar un objeto para cada pregunta.
! ====================================

[ QueryTopic;
  switch (scope_stage) {
	  1: rfalse;
	  2: ScopeWithin(WhatIsTopics); rtrue;
	  3: "No tienes ni idea.";
  }
];

[ QuerySub; noun.description();];

Verb 'que' * 'es'/'era' scope=QueryTopic -> Query;

Class MultiTopic
	with
		number 0,
		parse_name [ _w1 _w2 _i _sw1 _sw2 _len;
			_w1 = NextWordStopped();
			_w2 = NextWordStopped();
			_i = 0;
			_len = self.#topics / 2;
#IfTrue RUNTIME_ERRORS > RTE_MINIMUM;
#IfTrue RUNTIME_ERRORS == RTE_VERBOSE;
			if(_len % 3 > 0)
				"ERROR: multi_topic topics property of ", (the) self," has incorrect # of values!^";
#IfNot;
			if(_len % 3 > 0)
				"ERROR: multi_topic #1!^";
#EndIf;
			while(_i < _len) {
				_sw1 = self.&topics-->(_i+2);
#IfTrue RUNTIME_ERRORS == RTE_VERBOSE;
				if(~~(_sw1 ofclass String or Routine))
					"ERROR: Element ", _i+2, " in multi_topic topics property of ", (the) self," is not a string or routine!^",
						"Element: ", (name) _sw1, "^";
#IfNot;
				if(~~(_sw1 ofclass String or Routine))
					"ERROR: multi_topic #2!^";
#EndIf;

				_i = _i + 3;
			}
			_i = 0;
#endif;
			while(_i < _len) {
				_sw1 = self.&topics-->_i;
				_sw2 = self.&topics-->(_i+1);
				if(_w1 == _sw1 && _w2 == _sw2) {
					self.number = _i;
					return 2;
				}
				if(_w1 == _sw1 or _sw2) {
					self.number = _i;
					return 1;
				}
				_i = _i + 3;
			}
			! Tendría sentido devolver 0 aquí, pero las rutinas de la propiedad
			! devuelven 0 por defecto de todos modos.
		],
		description [ _k;
			_k = self.&topics-->(self.number + 2);
			if(_k ofclass Routine) {
				_k();
				rtrue;
			}
			print_ret (string) _k;
		];

Object WhatIsTopics "WhatIsTopics";

MultiTopic -> WhatIsTopic_1 "tema"
	with
		topics
			1 'biblioteca' "Una biblioteca es un lugar donde se guardan libros y la gente puede tomarlos prestados."
			1 'grue' "Un grue es una criatura siniestra que acecha en la oscuridad. Si alguien ve realmente a un grue,
				no vivirá lo suficiente para contarlo."
			1 'encargado' "Los encargados de las bibliotecas supervisan las actividades y funciones diarias de una biblioteca.";

! ====================================
! Las habitaciones y los objetos del juego empiezan aquí.
! ====================================

Constant ArtNoveauDesc "Es un edificio precioso. Parece arquitectura Art Nouveau del siglo XIX.";

Object GloomyStreet "Calle Sombría"
	with
		description
			"Las casas de esta zona parecen abandonadas precipitadamente, como si una misteriosa tragedia hubiera sembrado
				pánico entre sus antiguos habitantes. Todo lo que ves son ventanas destrozadas y puertas arrancadas.
				Frente a ti, al sur, está el extrañamente bien conservado y hermoso
				edificio de la biblioteca donde tienes que trabajar.",
		cheap_scenery
			1 'edificio' ArtNoveauDesc
			CS_FEM_THEM 'ventanas' 'puertas' "¡Restos de una tragedia lejana y misteriosa!"
			CS_FEM 'casas//p' 'calle' "Casas sombrías y abandonadas."
			CS_FEM 1 'biblioteca' ArtNoveauDesc,
		s_to Library,
		cant_go "Tu trabajo está al sur, aunque estás tentado de volver a casa y olvidarte de todo.",
		after [;
			Go:
				if(FlagIsSet(F_STARTED_FIRE))
					StopDaemon(EvilBook);
				if(FlagIsSet(F_BOOK_DELIVERED) || FlagIsSet(F_STARTED_FIRE)) {
					Achieved(TASK_LEAVE);
					<Look>;
					deadflag = 2;
					if(FlagIsSet(F_BOOK_DELIVERED))
						"^Sales de la biblioteca con sentimientos encontrados. Por un lado, te alegras
							de haber terminado tu modesta tarea y de salir de este tétrico barrio.
							Por otro, hay algo en esa biblioteca que te pone los pelos de punta.
							de punta.
							^^En fin. Quizás sea mejor olvidarlo y disfrutar de una tarde tranquila en tu piso.";
					else
						"^¡Has conseguido escapar del edificio en llamas justo a tiempo! Mientras recuperas el aliento
							sobre las baldosas de la calle, observas las paredes ennegrecidas por las llamas, la puerta cayéndose
							a pedazos y las ventanas estallando por el calor.
							^^ Te llega un sonido, lejano pero perceptible, más allá del crepitar de las llamas.
							Es una risa siniestra que resuena en las calles solitarias, la risa del 
							encargado de la biblioteca.
							^^ Aterrorizado, te alejas de aquel lugar, para no volver jamás. En el futuro,
							tendrás más cuidado con los trabajos que elijas.";
				}
		],
	has light;

Object Library "La biblioteca"
	with
		description [;
			if(FlagIsSet(F_STARTED_FIRE))
				"Contemplas horrorizado cómo las llamas alcanzan los libros, trepan por las estanterías
					y convierten cada volumen en una bola de fuego. Al este se halla el interior
					del edificio, y al norte, se puede escapar al exterior.";
			"La biblioteca parece sorprendentemente grande por dentro. Decenas de miles de valiosos
			volúmenes antiguos. El aire parece cargado de polvo y olor a antigüedad.
			^^ Al este, es posible adentrarse en el edificio. Al norte está la salida al exterior.";
		],
		cheap_scenery
			CS_THEM 'volumenes' 'libros' "Uno podría pasarse toda la vida leyendo sólo una parte de los libros de esta sala."
			CS_FEM_THEM 1 'estanterias' "Llenas de todos los libros imaginables de este mundo."
			CS_FEM 1 'biblioteca' "Es una hermosa biblioteca antigua.",
		n_to GloomyStreet,
		e_to TopOfStairs,
		cant_go "Te encantaría explorar la biblioteca, pero ahora tienes otras cosas en la cabeza.",
	has light;

Object -> Manager "encargado de la biblioteca"
	with
		article "el",
		description
			"Aunque a primera vista parece un hombre normal de mediana edad, hay algo
				inquietante en esta persona. Tal vez sea el hecho de que viste de negro o
				el constante movimiento de sus ojos y sus susurros para sí mismo. O tal vez sean
				los símbolos misteriosos y arcanos que cuelgan de todo su cuerpo.",
		describe [;
			print "^El encargado de la biblioteca está aquí, profundamente concentrado en la disposición de una de las estanterías";
			if(FlagIsClear(F_TALKED_TO_MANAGER)) print ". Tal vez deberías hablar con él y preguntarle sobre el trabajo";
			".";
		],
		name 'encargado',
		before [;
			TalkTo:
				if(FlagIsClear(F_TALKED_TO_MANAGER)) {
					SetFlag(F_TALKED_TO_MANAGER);
					Achieved(TASK_TALK_MANAGER);
					"Levanta la vista de la estantería sorprendido. Cuando te ve, sonríe (hay
						algo inquietante en su sonrisa) y dice: ~¡Vaya! Olvidé que venías
						hoy. Eres el nuevo ayudante, ¿verdad? Te imaginaba más alto. No te preocupes.
						No podré pasar mucho tiempo contigo hoy. Será mejor que vengas mañana.
						^^Justo cuando estás a punto de salir de la biblioteca con irritación, escuchas
						su voz de nuevo. ~¡Espera! Tal vez sí te necesite. Hay un libro que necesito,
						Donde viven los monstruos de Maurice Sendak. Debo haberlo dejado en el taller
						del sótano. Frank el Robot está allí abajo. Él te ayudará a encontrarlo.
						^^Después de decir esto, te da la espalda y sigue arreglando la estantería.
						^^(¿Ha dicho algo sobre... un robot?)";
				} else
					"~¿No ves que estoy ocupado? ¡No puedo perder el tiempo con tonterías! ¡Pídele ayuda a Frank!
						^^Una vez más, dirige su atención a la fascinante estantería.";
		],
		life [;
			Ask, Tell, Show:
				if(noun==WildThingsBook)
					<<Give noun self>>;
				<<TalkTo self>>;
			Give:
				if(noun==WildThingsBook) {
					remove noun;
					scope_modified = true;
					SetFlag(F_BOOK_DELIVERED);
					Achieved(TASK_GIVE_BOOK);
					"Cuando te acercas al encargado, por un momento parece dispuesto a enfadarse por
						la interrupción, pero su cara se ilumina cuando ve el libro en tu mano.
						^^~¡Genial! Has hecho exactamente lo que te pedí.~
						^^En confianza, añade: ~Me alegro de que no hayas perdido el tiempo con cosas que
						no son de tu incumbencia. Eso me hace confiar más en ti.~
						^^Te da dos palmaditas en la espalda y te dice: ~Ven mañana a primera hora. Empezaremos a trabajar de verdad.~
						Y de nuevo se vuelve hacia la estantería.";
				}
		],
	has animate;

Object TopOfStairs "Escaleras descendentes"
	with
		description [;
			if(FlagIsSet(F_STARTED_FIRE))
				"El fuego ha alcanzado la pequeña y polvorienta habitación. Las cajas y su contenido están ardiendo
					también. Desde la escalera y desde la biblioteca hacia el oeste llega el sonido del
					crepitar de las llamas. ¿Cómo ha podido propagarse tan rápidamente?";
			"Es una habitación pequeña y polvorienta, con algunas cajas apiladas y una escalera que baja al
			sótano oscuro. Un haz de luz viene del oeste y conduce a la sala principal de la biblioteca.";
		],
		cheap_scenery
			1 'haz' "El haz de luz destaca claramente en la oscuridad de la habitación."
			CS_FEM 1 'luz' "El haz de luz destaca claramente en la oscuridad de la habitación."
			CS_FEM 'caja' 'cajas\\p' "Las cajas están llenas de libros esperando a ser colocados en sus estanterías. Aquí no hay
							nada que te interese."
			CS_FEM 'escalera' 'escaleras\\p' "La escalera conduce a la oscuridad del sótano.",
		before [;
			Go:
				if(selected_direction==d_to) {
					if(FlagIsClear(F_TALKED_TO_MANAGER))
						"No tienes nada que hacer merodeando por el sótano. Mejor ve a hablar con el
							encargado de la biblioteca, para saber dónde te necesitan.";
					if(FlagIsSet(F_BOOK_DELIVERED))
						"Has terminado por hoy y el director de la biblioteca te ha dicho que te vayas a casa. Mejor
							no vayas a husmear por el sótano.";
					if(self hasnt general) {
						give self general;
						QuoteBox(quote_stairs);
					}
				}
		],
		w_to Library,
		d_to BottomOfStairs,
	has light;

Object BottomOfStairs "Sótano, junto a escalera"
	with
		description [;
			if(FlagIsSet(F_STARTED_FIRE))
				"Las paredes del pasillo arden ferozmente, desde la escalera que sube a la habitación
					que da al este. El calor es sofocante.";
			"Aquí está casi oscuro, ya que a la lámpara del techo le falta la bombilla.
				Sin embargo, hay suficiente luz proveniente de la escalera para distinguir
				tus alrededores. Hay más luz hacia el este.";
		],
		cheap_scenery
			CS_FEM 'escalera' 'escaleras\\p' "La escalera conduce a la oscuridad del sótano.",
		u_to TopOfStairs,
		e_to Hallway,
	has light;

! Datos que Frank necesita para imprimir de dónde viene.
#IfDef OPTIONAL_FULL_DIRECTIONS;
Array opposite_directions static table "el sur" "el norte" "el oeste" "el este" "el sureste" "el suroeste" "el noroeste" "el noreste"
	"abajo" "arriba" "el exterior" "el interior";
#IfNot;
Array opposite_directions static table "el sur" "el norte" "el oeste" "el este" "abajo" "arriba" "el exterior" "el interior";
#EndIf;

Object -> Frank "Frank el robot"
	with
		number 0,
		name 'frank' 'el' 'robot',
		when_off [; ! La primera vez que el jugador ve a Frank, se enciende automáticamente y se inicia su demonio.
			give self on;
			give self moved;
			StartDaemon(self);
			"Frank el Robot está aquí de pie, todo oscuro y silencioso. Al entrar en la habitación,
				algunos LEDs se encienden, y los ojos de Frank empiezan a brillar con una débil luz amarilla.
				Su voz empieza a sonar con una extraña cortesía metálica. ~¡Hola, señor!
				Usted debe de ser el nuevo empleado, ¿verdad? Mi nombre es Frank, abreviatura de Frankfurter
				Gelenkroboter 2100. Pero, por favor, llámeme Frank. Estoy aquí para ayudarle
				en su búsqueda del libro.~";
		],
		before [;
			TalkTo:
				"~Mis disculpas, señor, mi comprensión del lenguaje hablado no es ideal, pero
					sin duda mejorarán este aspecto en la próxima actualización del software.~";
			SwitchOff:
				"Un arco de luz brillante sobresale cerca del botón y la descarga eléctrica te hace
					saltar hacia atrás. ~No se lo aconsejo, señor.~";
			Cover:
				if(second==Box) {
					if(child(Box))
						"¡Pero la caja no está vacía!";
					SetFlag(F_FRANK_BLOCKED_BY_BOX);
					move Box to self;
					scope_modified = true;
					Achieved(TASK_BLOCK_ROBOT);
					self.number = 3; ! Haz que la caja permanezca en su sitio estos movimientos menos uno.
					"Aprovechas un momento en que Frank no mira en tu dirección
						y colocas rápidamente la caja sobre él. El robot pierde la calma y empieza
						a girar, mientras su voz suena apagada desde el interior. ~¡Señor, parece que
						hay un problema con mis sensores! ¡Deme unos momentos para arreglarlo!~";
				}
				"No parece que encaje.";
		],
		react_before [;
			Go:
				if(location == StorageRoom && selected_direction == e_to && FlagIsClear(F_FRANK_BLOCKED_BY_BOX)) {
					print "Frank aparece delante de ti, bloqueándote el paso. ";
					if(self hasnt general) {
						give self general;
						"~Oh, señor, perdone mi rudeza, pero le ruego que no entre en esta habitación.
							No es segura. Y además, no hay nada inusual que ver
							allí. Nada en absoluto. Es una habitación increíblemente aburrida.~
							Su voz se vuelve profunda y amenazante por un segundo. ~Así que
							le recomiendo que no insista.~";
					}
					"~Por favor, señor, no insista. No hay nada que ver allí.~";
				}
		],
		daemon [ _my_loc _i _dir_prop _dir_count;
			! Frank puede querer decir algo sobre la caja
			if(FlagIsSet(F_FRANK_BLOCKED_BY_BOX)) {
				if(Frank in location)
					print "^Frank da vueltas como loco tratando de liberarse en vano.^";
				if(--self.number == 0) {
					ClearFlag(F_FRANK_BLOCKED_BY_BOX);
					remove Box;
					scope_modified = true;
					if(Frank in location)
						"^De uno de los brazos del robot sale una especie de cuchilla que corta la caja
							por la mitad. Una vez liberado, eleva otro de sus brazos, y sale una llama de
							fuego que convierte el cartón en cenizas.
						^^~¡Oh, jajaja, no se lo va a creer, señor! Parece que tenía una caja de 
							cartón encima de mí. ¡Qué descuido más tonto por mi parte!~";
					else
						"^Desde otra habitación, se oye el sonido de la caja de cartón cayendo
							al suelo, y a continuación algo que suena como una rápida ráfaga de fuego.";
				}
			}
			! Frank quiere seguir al jugador, pero no puede subir/bajar/entrar/salir
			if(FlagIsClear(F_FRANK_BLOCKED_BY_BOX)) {
				_my_loc = parent(self);
				if(location ~= _my_loc){
					_dir_count = DIRECTION_COUNT - 4; ! Saltar las 4 últimas direcciones (u_to, d_to, in_to, out_to)
					for(_i=1 : _i <= _dir_count : _i++) {
						_dir_prop = direction_properties_array->_i;
						if(_my_loc provides _dir_prop && _my_loc._dir_prop == location) {
							move self to location;
							scope_modified = true;
							print "^Frank entra desde ", (string) opposite_directions-->_i, ".^";
							if (location == RepairShop && WildThingsBook in Box && Box in location &&
									FlagIsClear(F_FRANK_HAS_SEEN_BOOK)) {
								SetFlag(F_FRANK_HAS_SEEN_BOOK);
								print "^~¡Oh, aquí está, señor! ¡El libro que pidió el maestro! Justo
									dentro de esa caja de cartón!~^";
							}
							if(location == DarkChamber) {
								print "^Cuando te ve dentro de la habitación, empieza a ponerse nervioso.
									~¡Oh, no, no, no, señor! ¡No está bien que esté aquí! No hay
									nada que ver aquí, de verdad. Permítame acompañarlo.~ Y te empuja fuera
									suave pero firmemente.^^";
								move self to StorageRoom;
								PlayerTo(StorageRoom);
							}
							rtrue;
						}
					}
				}
			}
		],
		life [;
			Ask, Tell, Show:
				<<TalkTo self>>;
		],
		description [;
			print "Frank parece un cruce entre un montón de tostadoras apiladas y un flemático
				mayordomo inglés. Algo te dice que es mejor no enfadarlo: Algunas partes de su
				anatomía se parecen mucho a armamentos.^";
			return 2;
		],
	has animate proper switchable transparent;

Object Hallway "Pasillo"
	with
		description [;
			if(FlagIsSet(F_STARTED_FIRE))
				"Las paredes del pasillo arden ferozmente, desde las escaleras al oeste hasta la habitación al
					sur. El calor es sofocante.";
			"Un amplio pasillo conduce desde las escaleras al oeste hasta otra habitación al sur. Hay
				algunas puertas polvorientas a lo largo de las paredes, pero todas han sido cerradas con clavos hace mucho tiempo.";
		],
		cheap_scenery
			CS_FEM 'puerta' 'puertas\\p' "Puertas normales, pero viejas y desgastadas, con algunas telarañas.",
		w_to BottomOfStairs,
		s_to StorageRoom,
	has light;

Object StorageRoom "Trastero"
	with
		description [;
			if(FlagIsSet(F_STARTED_FIRE)) "El fuego ha llegado también al almacén,
				convirtiendo las telarañas y el polvo en cenizas. ";
			else print "Un gran almacén, que ahora está vacío salvo por las telarañas y el polvo. ";
			"Algo de luz viene del norte y del oeste. Un pasadizo más pequeño y oscuro conduce al este. Hay un
				letrero amarillo sobre el pasadizo oscuro.";
		],
		before [;
			Go:
				if(selected_direction==e_to && self hasnt general) {
					give self general;
					QuoteBox(quote_darkchamber);
				}
		],
		cheap_scenery
			1 'polvo' "No parece que esta parte del sótano se limpie nunca."
			'letrero' 'amarillo' "~¡Zona peligrosa! ¡No se acerquen!~"
			CS_FEM_THEM 1 'telaranas' "No parece que esta parte del sótano se limpie nunca.",
		n_to Hallway,
		w_to RepairShop,
		e_to DarkChamber,
	has light;

Object RepairShop "Taller de reparaciones"
	with
		description [;
			if(FlagIsSet(F_STARTED_FIRE)) "Aquí, también, el banco de trabajo ha empezado a arder, junto con las paredes y las herramientas.
				La única salida es hacia el este.";
			"Se trata de una sala más pequeña, con un banco de trabajo y diversas herramientas especializadas. Toda la habitación
				está bastante limpia. Hay una salida al este.";
		],
		cheap_scenery
			'banco' 'trabajo' "Es un banco de trabajo viejo y desgastado."
			CS_FEM_THEM 'herramientas' 'especializadas' "Las herramientas son para reparar libros, eso se nota (pero no mucho más que eso).",
		e_to StorageRoom,
	has light;

Object -> Box "caja de cartón"
	with
		name 'caja' 'de' 'carton',
		description "Es una caja vieja y desgastada, aunque en bastante buen estado. Parece bastante resistente.",
	has container open female;

Object -> -> WildThingsBook "libro pequeño"
	with
		name 'libro' 'pequeno' 'libros//p',
		description "Donde viven los monstruos, de Maurice Sendak.",
		before [;
			Open, Close:
				"El encargado acaba de pedirte que traigas el libro, no que lo leas.";
		];

Object DarkChamber "Cámara oscura"
	with
		description [;
			if (self hasnt general) {
				give self general;
				print "Al entrar en la sala, dos velas sobre una mesa se encienden espontáneamente, como por arte de magia.^^";
			}
			if(FlagIsSet(F_STARTED_FIRE))
				"La mesa, el libro y las velas están ahora envueltos en fuego. Largas lenguas de fuego
					corren por las paredes pintadas de negro. La única salida es hacia el oeste.";
			print "Es una habitación muy inquietante. Todas las paredes están pintadas de negro, con extraños
				símbolos dorados. En el centro de la habitación hay una gran mesa redonda cubierta con un mantel de
				terciopelo negro. Sobre ella descansa un inmenso libro rojo ";
			if (EvilBook has open) print "abierto";
			else print "cerrado";
			". A ambos lados del libro, dos velas rojas inundan la habitación con el olor a
				cera ardiente. La única salida está hacia el oeste.";
		],
		cheap_scenery
			CS_THEM 'simbolo' 'simbolos' "Estos símbolos podrían ser letras de alguna escritura antigua. No lo sabes."
			'mantel' 'terciopelo' "Es pesado y completamente negro."
			CS_FEM 'pared' 'paredes//p' "Las paredes absorben tanta luz que casi parece que los símbolos dorados flotan en el vacío.",
		w_to StorageRoom,
	has light;

Object -> Table "mesa"
	with
		name 'mesa' 'redonda',
		description "Es una mesa de madera muy robusta cubierta con un mantel de terciopelo negro.",
	has static scenery female;

Constant BurntDesc "Cuando te falta el aliento y el calor te hace perder el conocimiento, lo último que oyes es una risa a lo lejos...";

Object -> EvilBook "libro rojo"
	with
		number 0,
		parse_name [ _w _i;
			while(true) {
				_w = NextWordStopped();
				if(_w == 'inmenso' or 'grueso' or 'pesado' or 'rojo' or 'piel' or 'libro' or 'libros//p' ||
					(_w == 'abierto' && self has open) || (_w == 'cerrado' && self hasnt open)) _i++;
				else
					break;
			}
			return _i;
		],
		describe [; rtrue; ],
		description [;
			if(FlagIsSet(F_STARTED_FIRE))
				"¡El libro está ardiendo! ¿Es tu imaginación o te parece oír un quejido
					más allá del crepitar del papel?";
			print "Echas un vistazo con inquietud al grueso y pesado libro encuadernado en cuero rojo. Tienes
				la sensación de que hay algo profundamente maligno en él. ";
			if(self has open)
				"Ahora está abierto, revelando páginas ricamente escritas con una meticulosa caligrafía. Unas pocas
					palabras parecen sobresalir del resto.";
			else
				"En una tipografía profusamente ornamentada, se pueden leer en su cubierta cerrada las palabras ~Summone daemoniorum~.";
		],
		before [;
			Take:
				"Intentas levantarlo de la mesa, pero te sorprende lo increíblemente pesado
					que es. Te sientes incapaz de moverlo ni un centímetro.";
			Burn:
				if(second==Candles or 0) {
					if(FlagIsSet(F_STARTED_FIRE)) {
						"¡Pero ya está ardiendo! Tienes que salir del edificio inmediatamente.";
					} else {
						SetFlag(F_STARTED_FIRE);
						Achieved(TASK_BURN_BOOK);
						StartDaemon(self);
						remove Manager;
						scope_modified = true;
						"Sin dudarlo un instante, arrancas una de las páginas y la quemas en una
							vela. Con la página ardiendo, prendes fuego al resto del libro.
							^^¡El fuego se extiende del libro a la habitación a toda velocidad! Antes de que puedas
							creerlo, las paredes negras arden con un fuego que no puede ser natural. ¿Qué
							clase de magia maligna es esta?";
					}
				}
		],
		daemon [;
			switch(++self.number) {
				2:
					if(location==DarkChamber)
						"^A medida que las llamas suben, el techo de madera también se incendia.";
					else
						"^El sonido del fuego en la cámara oscura se intensifica de repente.";
				6:
					if(location==Library or TopOfStairs)
						"^Las llamas surgen del suelo en una esquina y rápidamente comienzan a extenderse por las paredes.";
					else
						"Ahora hace un calor insoportable aquí abajo.";
				8:
					if(location==Library or TopOfStairs)
						"^Las llamas te rodean ahora. Sin embargo, deberías poder llegar a la salida.";
				11:
					deadflag=4;
					"¡Las llamas cubren todas las salidas! El fuego se cierne a tu alrededor con una ferocidad antinatural. ", (string) BurntDesc;
			}
			if(self.number > 7 && location ~= Library or TopOfStairs) {
				deadflag = 4;
				"El calor es demasiado para ti y te desplomas en el suelo. ", (string) BurntDesc;
			}
		],
		add_to_scope [;
			if(self has open)
				PlaceInScope(Words);
		],
	has openable transparent static;

Object -> Candles "velas"
	with
		description "Dos velas rojas, ardiendo a ambos lados del libro.",
		describe [; rtrue; ],
		article "un par de",
		name 'vela' 'velas',
		before [;
			Take:
				"Las velas no se mueven.";
			Burn:
				"Las velas ya están encendidas.";
		],
	has pluralname static female;

Object Words "palabras"
	with
		name 'tetricas' 'letras' 'palabra' 'palabras',
		before [;
			Examine:
				deadflag = 3;
				"Después de unos segundos intentando descifrar la antigua caligrafía, logras distinguir las
					palabras y leerlas en voz alta: ~KLAATU BARADA NIKTO~^^
					De repente, un temblor comienza a sentirse desde el suelo, extendiéndose por las paredes.
					Sientes un escalofrío en la espalda y te das la vuelta. No puedes creer lo que ves...^^
					El suelo de la habitación se ha transformado en un sumidero infinitamente profundo con
					paredes de piedra negra. Escalando las paredes, puedes ver docenas... No, ¡cientos!
					de criaturas de piel azulada y ojos rojos, mirándote con hambre depredadora.^^
					Van a salir. Y tú las has liberado.";
		],
	has female pluralname;

! ====================================
! Por último, la rutina Initialise.
! ====================================

[Initialise;
	lookmode = 2; ! Modo largo por defecto
	"^^La búsqueda de empleo puede ser ardua para una persona joven como tú, sin experiencia ni contactos y
		recién llegada a la ciudad. Por eso, cuando ayer tuviste el periódico en tus manos y leíste
		el anuncio (~se necesita una persona joven e inexperta para ayudar en una biblioteca~) no lo dudaste ni un segundo.
		^^Sin embargo, ahora que caminas por las lúgubres calles del barrio donde reside tu futuro
		trabajo, te preguntas si no te precipitaste al aceptar...^";
];
