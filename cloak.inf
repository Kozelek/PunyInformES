!% -Cu
!% -~S
!% $OMIT_UNUSED_ROUTINES=1
!% $ZCODE_LESS_DICT_DATA=1

! The very first lines of the main source code file for a game can
! contain compiler options, like the lines above. -~S disables
! strict error checking. This is otherwise used in z5 and z8 games by
! default. While useful for debugging, it adds ~10 KB to the story file
! size and it makes the game slower.
! $OMIT_UNUSED_ROUTINES=1 makes the compiler remove all routines which
! aren't used. This can save some space.

! ============================================================================ !
!   Cloak of Darkness - a simple demonstration of Interactive Fiction
!	   This version for INFORM written by Roger Firth on 17Sep99
! ============================================================================ !


Constant Story	  "La capa de oscuridad";
Constant Headline   "^Una demostración básica de FI.^";
Constant MAX_SCORE  2;

Release 3;
Serial "221116";

Constant STATUSLINE_SCORE; Statusline score;
Constant INITIAL_LOCATION_VALUE = foyer;

[ DeathMessage; print "Has perdido"; ];


Include "globals.h";
Include "puny.h";

! ============================================================================ !

Object  foyer "Vestíbulo de la Ópera"
  with  description
		   "Te encuentras en un amplio vestíbulo, espléndidamente decorado en rojo
			y dorado, con brillantes lámparas de araña. La entrada desde
			la calle está al norte, y hay puertas al sur y al oeste.",
		s_to  bar,
		w_to  cloakroom,
		n_to
		   "Acabas de llegar, y además, el tiempo fuera
			parece estar empeorando.",
  has   light;

Object  cloakroom "Guardarropa"
  with  description
		   "Las paredes de esta pequeña habitación parecen haber estado alguna vez
		    cubiertas de ganchos, aunque ahora sólo queda uno. La salida es una
			puerta al este.",
		e_to  foyer,
  has   light;

Object  hook "gancho pequeño de latón" cloakroom
  with  name 'gancho' 'pequeno' 'laton',
		description [;
			print "Es sólo un pequeño gancho de latón, ";
			if (self == parent(cloak)) "con una capa colgando de él.";
			"atornillado a la pared.";
		],
  has   scenery supporter;

Object  bar "Bar del vestíbulo"
  with  description
		   "El bar, mucho más tosco de lo que habría imaginado tras la opulencia
			del vestíbulo al norte, está completamente vacío. Parece haber
			algún tipo de mensaje garabateado en el serrín del suelo.",
		n_to  foyer,
		before [ _test;
			Look, Inv, Going:
				rfalse;
			Go:
				_test = self hasnt light && selected_direction ~= n_to;
				if (_test) {
					message.number = message.number + 2;
					"Andar dando tumbos en la oscuridad no es una buena idea.";
				}
			default:
				if (self hasnt light) {
					message.number = message.number + 1;
					"¿En la oscuridad? ¡Podrías tirar algo fácilmente!";
				}
		],
  has   ~light;

Object  cloak "capa de terciopelo"
  with
		parse_name [ _words;
			while(NextWord() == 'capa' or 'de' or 'hermosa' or 'oscura' or 'negra' or 'tercipelo') _words++;
			return _words;
		],
!		name 'handsome' 'dark' 'black' 'velvet' 'satin' 'cloak',
		description
		   "Una hermosa capa, de terciopelo ribeteada de raso, y ligeramente
			salpicada de gotas de lluvia. Su negrura es tan profunda que
			casi parece absorber la luz de la habitación.",
		before [;
			Drop, PutOn:
				if (location ~= cloakroom)
				   "Este no es el mejor lugar para dejar una capa elegante
					tirada por ahí.";
		],
		after [;
			Take: give bar ~light;
			Drop, PutOn:
				if (location == cloakroom) {
					give bar light;
					if (action == ##PutOn && self has general) {
						give self ~general;
						score++;
						}
					}
		],
  has   clothing general female;

Object  message "mensaje garabateado" bar
  with  name 'mensaje' 'serrin' 'suelo' 'garabato',
		description [;
			if (self.number < 2) {
				score++;
				deadflag = 2;
				print "El mensaje, claramente marcado en el serrín, dice...";
				}
			else {
				deadflag = 3;
				print "El mensaje ha sido pisoteado por descuido, dificultando su lectura.
				  Sólo se distinguen las palabras...";
				}
		],
		number  0,
  has   scenery;

[ Initialise;
	move cloak to player;
	give cloak worn;
   "^^A través de la lluviosa noche de noviembre, te alegras de ver las
	luces brillantes de la Ópera. Es sorprendente que no haya más gente
	pero, oye, ¿qué esperas en un juego de demostración de bajo
	presupuesto...?^^";
];


! ============================================================================ !


Verb 'cuelga' 'colgar' * held 'en' / 'de' noun	-> PutOn;

! ============================================================================ !
